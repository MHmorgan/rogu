#!/usr/bin/env zsh

# Testing script for Rogu
#
# Usage: ./test [testname...]
#
# Explicitly tests:
#   - ugor commands
#   - install
#   - upload
#   - sync
#   - update
#
# Implicitly tests:
#   - get (via upload)
#


export ROGU_APP_DIR=testappdir

DIR=testdir

# To Ugor
LOCAL_01=$DIR/01local.txt
LOCAL_02=$DIR/02local.json

# From Ugor
REMOTE_01=$DIR/01remote.txt
REMOTE_02=$DIR/02remote.json

SCRATCH=$DIR/scratch

alias run_rogu='venv/bin/python3 -OO rogu'
alias test_rogu='venv/bin/python3 rogu'


# ------------------------------------------------------------------------------
# HELPERS

autoload -U colors && colors

function exists   { which $* &>/dev/null }
function ifexists { which $1 &>/dev/null && $* }

exists gum || {
    echo "[!!] gum command not found"
    exit 1
}

function warn    { echo "$fg_bold[yellow]>>> $*$reset_color" }
function err     { echo "$fg_bold[red]>>> $*$reset_color" }
function log     { echo "\n$fg_bold[default]>>> $*$reset_color" && sleep 0.2 }
function bold    { echo "$fg_bold[default]$*$reset_color" }
function success { echo "$fg_bold[green]>>> Success <<<$reset_color" }

function wait {
    gum spin --spinner=points --title=$2 sleep $1
}

function header {
    echo
    gum style \
        --border=rounded \
        --border-foreground="#ff6d67" \
        --width=20 \
        --align=center "$*"
}

function fail {
    echo "\n$fg_bold[red]FAIL: $*$reset_color"
    if [[ "$FAIL_DUMP" == 1 ]]; then
        dump_history_resources
    fi
    exit 1
}

function assert_equal {
    test -f $1 || fail "File $1 does not exist"
    test -f $2 || fail "File $2 does not exist"
    if [[ -n "$(diff $1 $2)" ]]; then
        diff $1 $2
        fail "Files $1 and $2 are not equal"
    fi
}

function dump_history_resources {
    bold "\nResources:"
    run_rogu resources
    bold "\nHistory:"
    run_rogu history
}


# ------------------------------------------------------------------------------
# SETUP

function setup {
    log "SETUP <<<"

    bold "\nCreating..."
    mkdir -pv $DIR
    mkdir -pv $ROGU_APP_DIR

    echo "Hello\nWorld\nFrom Rogu\n" > $LOCAL_01
    echo '{"Hello": "World", "From": "Rogu"}' > $LOCAL_02
}


# ------------------------------------------------------------------------------
# TEARDOWN

function teardown {
    log "TEARDOWN <<<"

    dump_history_resources

    bold "\nRemoving..."
    rm -rfv $DIR
    rm -fv $ROGU_APP_DIR/rogu-history.csv
    rm -fv $ROGU_APP_DIR/rogu-resource-cache.db
    FAIL_DUMP=0

    # Remove test files from ugor
    local LIST=$(run_rogu ugor list)
    for FILE in $REMOTE_01 $REMOTE_02; do
        if [[ $LIST =~ $FILE ]]; then
            run_rogu ugor delete $FILE
        fi
    done
}


# ------------------------------------------------------------------------------
# UGOR

function ugor {

    wait 3 "Ugor stuff..."

    log "Creating file 01..."
    test_rogu ugor put $LOCAL_01 $REMOTE_01 \
        --description='File 01' \
        --tag='test tag' \
        --tag2='test tag 2' \
        --tag3='test tag 3' \
        --data='{"test": "data"}' \
        --data2='{"test": "data 2"}' \
        --data3='{"test": "data 3"}' \
        --data4='{"test": "data 4"}' \
        --data5='{"test": "data 5"}' ||
        fail 'Failed to upload file 01'
    success

    log "Getting file 01..."
    test_rogu ugor get $REMOTE_01 || fail 'Failed to download file 01'
    success

    log "Deleting file 01..."
    test_rogu ugor delete $REMOTE_01 || fail 'Failed to delete file 01'
    success

    log "Getting non-existent file 01..."
    test_rogu ugor get $REMOTE_01 && fail 'Got non-existent file 01'
    success

    # TODO Test Ugor's find functionality
}


# ------------------------------------------------------------------------------
# INSTALL

function install {  # Rename to install-file?

    # Rogu file

    wait 3 "Rogu files..."

    log "Installing non-existent file"
    test_rogu install $REMOTE_01 $REMOTE_01 &&
    fail "Installed non-existent file unexpectedly"
    success

    log "Installing from invalid Rogu URI"
    test_rogu install $REMOTE_01 https://foobar.org/rogu &&
    fail "Installed from invalid Rogu URI unexpectedly"
    success

    log "Installing file"
    run_rogu ugor put $LOCAL_01 $REMOTE_01
    test_rogu install $REMOTE_01 $REMOTE_01 ||
    fail "Failed to install file"
    assert_equal $LOCAL_01 $REMOTE_01
    success

    log "Installing file again"
    test_rogu install $REMOTE_01 $REMOTE_01 &&
    fail "Installed file again unexpectedly"
    success

    log "Modifying and installing file"
    echo "Something else" > $REMOTE_01
    test_rogu install $REMOTE_01 $REMOTE_01 &&
    fail "Wrote over modified file unexpectedly"
    success

    log "Remove and install file"
    rm $REMOTE_01
    test_rogu install $REMOTE_01 $REMOTE_01 &&
    fail "Installed file unexpectedly"
    success

    log "Force install file"
    test_rogu install --force $REMOTE_01 $REMOTE_01 ||
    fail "Failed to force install file"
    success

    log "Install file with different name"
    test_rogu install $REMOTE_02 $REMOTE_01 ||
    fail "Failed to install file with different name"
    success

    log "Install other file with same name"
    run_rogu ugor put $LOCAL_02 $REMOTE_02
    test_rogu install $REMOTE_02 $REMOTE_02 &&
    fail "Installed other file with same name unexpectedly"
    success

    log "Force install other file with same name"
    test_rogu install --force $REMOTE_02 $REMOTE_02 ||
    fail "Failed to force install other file with same name"
    assert_equal $LOCAL_02 $REMOTE_02
    success

    # TODO Test install archive
    # wait 3 "Rogu archives..."
    # TODO Test install repo
    # wait 3 "Rogu repos..."
}


# ------------------------------------------------------------------------------
# UPLOAD

function upload {

    # Rogu file

    wait 3 "Rogu files..."

    log "Uploading non-existent file"
    test_rogu upload idont/exist $REMOTE_01 &&
    fail "Uploaded non-existent file unexpectedly"
    success

    log "Uploading to invalid Rogu URI"
    test_rogu upload $LOCAL_01 https://foobar.org/rogu &&
    fail "Uploaded to invalid Rogu URI unexpectedly"
    success

    log "Uploading file"
    test_rogu upload $LOCAL_01 $REMOTE_01 ||
    fail "Failed to upload file"
    success

    log "Uploading file again"
    test_rogu upload $LOCAL_01 $REMOTE_01 &&
    fail "Uploaded file again unexpectedly"
    success

    log "Get file and check content"
    test_rogu get $SCRATCH $REMOTE_01 ||
    fail "Failed to get file"
    assert_equal $LOCAL_01 $SCRATCH
    success

    log "Modify and upload file"
    echo "Something else" > $LOCAL_01
    test_rogu upload $LOCAL_01 $REMOTE_01 ||
    fail "Failed to upload modified file"
    success

    log "Upload a different file with the same name"
    test_rogu upload $LOCAL_02 $REMOTE_01 &&
    fail "Uploaded a different file with the same name unexpectedly"
    success

    log "Force upload a different file"
    test_rogu upload --force $LOCAL_02 $REMOTE_01 ||
    fail "Failed to force upload a different file"
    success

    log "Get file and check content"
    test_rogu get $SCRATCH $REMOTE_01 ||
    fail "Failed to get file"
    assert_equal $LOCAL_02 $SCRATCH
    success

    log "Upload first file w/modifications"
    echo "Lorem ipsum..." > $LOCAL_01
    test_rogu upload $LOCAL_01 $REMOTE_01 &&
    fail "Uploaded modified first file unexpectedly"
    success

    # TODO Test upload archive
    # wait 3 "Rogu archives..."
}


# ------------------------------------------------------------------------------
# SYNC

function sync {

    # Rogu file

    wait 3 "Rogu files..."

    log "Syncing non-existent file"
    test_rogu sync idont/exist $REMOTE_01 &&
    fail "Synced non-existent file unexpectedly"
    success

    log "Syncing to invalid Rogu URI"
    test_rogu sync $LOCAL_01 https://foobar.org/rogu &&
    fail "Synced to invalid Rogu URI unexpectedly"
    success

    log "Syncing file from local to remote"
    test_rogu sync $LOCAL_01 $REMOTE_01 ||
    fail "Failed to sync file from local to remote"
    success

    log "Syncing file from local to remote again"
    test_rogu sync $LOCAL_01 $REMOTE_01 &&
    fail "Synced file from local to remote again unexpectedly"
    success

    local FILE1=$DIR/file1

    log "Syncing file from remote to local"
    test_rogu sync $FILE1 $REMOTE_01 ||
    fail "Failed to sync file from remote to local"
    assert_equal $LOCAL_01 $FILE1
    success

    log "Syncing file from remote to local again"
    test_rogu sync $FILE1 $REMOTE_01 &&
    fail "Synced file from remote to local again unexpectedly"
    success

    log "Modify and sync file from local to remote"
    echo "Something else" > $LOCAL_01
    test_rogu sync $LOCAL_01 $REMOTE_01 ||
    fail "Failed to sync modified file from local to remote"
    success

    log "Syncing file from remote to local"
    test_rogu sync $FILE1 $REMOTE_01 ||
    fail "Failed to sync file from remote to local"
    assert_equal $LOCAL_01 $FILE1
    success

    # TODO Test archive sync
    # TODO Test repo sync
}


# ------------------------------------------------------------------------------
# UPDATE

# TODO Test update functionality


# ------------------------------------------------------------------------------
# RUN TESTS

if ! [[ -d venv ]]; then
    err "No virtual environment found"
    exit 1
fi

ALL_COMMANDS=(ugor install upload)

# Determine which tests to run_rogu
if (( $# == 0 )); then
    COMMANDS=$(gum choose --no-limit \
        --header="Choose tests:" \
        --selected=$(echo $ALL_COMMANDS | tr ' ' ',') \
        ${=ALL_COMMANDS})
    [[ -n "$COMMANDS" ]] || exit 0
elif [[ $@ == "all" ]]; then
    COMMANDS=$ALL_COMMANDS
else
    COMMANDS=$@
fi

# Clean up after failed tests
[[ -d $DIR ]] && teardown

for CMD in ${=COMMANDS}; do
    # Some sanity checks
    if ! [[ $(test_rogu resources) =~ "No resources" ]]; then
        test_rogu resources
        fail "There shouldn't be any resources yet"
    fi
    if ! [[ $(test_rogu history) =~ "No history" ]]; then
        test_rogu history
        fail "There shouldn't be any history yet"
    fi
    FAIL_DUMP=1

    # Run the test
    setup
    header $(echo $CMD | tr '[:lower:]' '[:upper:]')
    $CMD
    teardown
done

echo "$fg_bold[green]\nAll tests passed! Have an awesome day! :)$reset_color"
