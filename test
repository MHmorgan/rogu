#!/usr/bin/env zsh

export ROGU_APP_DIR=testappdir

DIR=testdir

# Test files with content
FILE1=$DIR/01local.txt
FILE2=$DIR/02local.json
# Test files without content
FILE3=$DIR/03local.txt
FILE4=$DIR/04local.txt
# Ugor files
RFILE1=$DIR/01remote.txt
RFILE2=$DIR/02remote.json

# Test directory with content
DIR1=$DIR/local-dir01
DIR2=$DIR/local-dir02
# Test directory without content
DIR3=$DIR/local-dir03
DIR4=$DIR/local-dir04
# Ugor directories
RARCHIVE1=$DIR/remote-dir01
RARCHIVE2=$DIR/remote-dir02

SCRATCH=$DIR/scratch

alias run_rogu='venv/bin/python3 -OO rogu'
alias test_rogu='venv/bin/python3 rogu'


# ------------------------------------------------------------------------------
# HELPERS

autoload -U colors && colors

function exists   { which $* &>/dev/null }
function ifexists { which $1 &>/dev/null && $* }

exists gum || {
    echo "[!!] gum command not found"
    exit 1
}

function warn    { echo "$fg_bold[yellow]>>> $*$reset_color" }
function err     { echo "$fg_bold[red]>>> $*$reset_color" }
function log     { echo "$fg_bold[default]>>> $*$reset_color" && sleep 0.2 }
function bold    { echo "$fg_bold[default]$*$reset_color" }
function success { echo "$fg_bold[green]>>> Success <<<$reset_color" }

function header {
    echo
    gum style \
        --border=rounded \
        --width=20 \
        --align=center "$*"
}

function fail {
    echo "\n$fg_bold[red]FAIL: $*$reset_color"
    if [[ "$FAIL_DUMP" == 1 ]]; then
        dump_resources
    fi
    exit 1
}

function failed {
    echo "\n$fg_bold[red]FAILED$reset_color"
    if [[ "$FAIL_DUMP" == 1 ]]; then
        dump_resources
    fi
    exit 1
}

function assert_equal {
    # Compare two files
    if [[ -f $1 && -f $2 ]]
    then
        if [[ -n "$(diff $1 $2)" ]]
        then
            diff $1 $2
            fail "Files $1 and $2 are not equal"
        fi

    # Compare two directories
    elif [[ -d $1 && -d $2 ]]
    then
        if [[ -n "$(diff -r $1 $2)" ]]
        then
            diff -r $1 $2
            fail "Directories $1 and $2 are not equal"
        fi

    else
        fail "Files $1 and $2 are not of the same type"
    fi
}

function assert_notequal {
    # Compare two files
    if [[ -f $1 && -f $2 ]]
    then
        if [[ -z "$(diff $1 $2)" ]]
        then
            fail "Files $1 and $2 are equal"
        fi

    # Compare two directories
    elif [[ -d $1 && -d $2 ]]
    then
        if [[ -z "$(diff -r $1 $2)" ]]
        then
            fail "Directories $1 and $2 are equal"
        fi

    else
        fail "Files $1 and $2 are not of the same type"
    fi
}

function dump_resources {
    bold "\nResources:"
    run_rogu resources
}


# ------------------------------------------------------------------------------
# SETUP

function setup {
    log "SETUP <<<"

    bold "\nCreating..."
    mkdir -pv $DIR
    mkdir -pv $ROGU_APP_DIR
    mkdir $DIR1
    mkdir $DIR2

    echo "Hello\nWorld\nFrom Rogu\n" > $FILE1
    echo '{"Hello": "World", "From": "Rogu"}' > $FILE2

    cp $FILE1 $FILE2 $DIR1
    cp $FILE1 $FILE2 $DIR2
}


# ------------------------------------------------------------------------------
# TEARDOWN

function teardown {
    log "TEARDOWN <<<"

    dump_resources

    bold "\nRemoving..."
    rm -rfv $DIR
    rm -rfv $ROGU_APP_DIR/file-cache
    rm -fv $ROGU_APP_DIR/rogu-resource-cache.db
    rm -fv $ROGU_APP_DIR/rogu-modified-cache.db
    FAIL_DUMP=0

    # Remove test files from ugor
    local LIST=$(run_rogu ugor list)
    local TARGETS=(
        $RFILE1
        $RFILE2
        $RARCHIVE1.tar.xz
        $RARCHIVE2.zip
    )
    for FILE in $TARGETS; do
        if [[ $LIST =~ $FILE ]]; then
            run_rogu ugor delete $FILE &&
            echo "$FILE from Ugor"
        fi
    done
}


# ------------------------------------------------------------------------------
# UGOR

function ugor {

    header "UGOR STUFF"

    log "Creating file 01"
    test_rogu ugor put $FILE1 $RFILE1 \
        --description='File 01' \
        --tag='test tag' \
        --tag2='test tag 2' \
        --tag3='test tag 3' \
        --data='{"test": "data"}' \
        --data2='{"test": "data 2"}' \
        --data3='{"test": "data 3"}' \
        --data4='{"test": "data 4"}' \
        --data5='{"test": "data 5"}' ||
        fail 'Failed to upload file 01'

    log "Getting file 01"
    test_rogu ugor get $RFILE1 || fail 'Failed to download file 01'

    log "Finding file 01"
    test_rogu ugor find --name "$DIR/01*.txt" || failed
    test_rogu ugor find --name-re "$DIR/\\d\\d\\w*.txt$" || failed
    test_rogu ugor find --tag 'test tag' || failed
    test_rogu ugor find --tag2 'test tag 2' || failed
    test_rogu ugor find --tag3 'test tag 3' || failed

    log "Deleting file 01"
    test_rogu ugor delete $RFILE1 || fail 'Failed to delete file 01'

    log "Getting non-existent file 01"
    test_rogu ugor get $RFILE1 && fail 'Got non-existent file 01'
    success

    header "BASICS"

    log "Uploading file 2"
    cat $FILE2 | test_rogu put $RFILE2 || failed

    log "Downloading file 2"
    test_rogu get $RFILE2 > $FILE3 || failed
    assert_equal $FILE2 $FILE3
    success
}


# ------------------------------------------------------------------------------
# FILE

function file {

    local URI1="ugor://file/$RFILE1"
    local URI2="ugor://file/$RFILE2"

    header "UPLOAD FILE"

    log "Uploading non-existent file"
    test_rogu upload idont/exist $URI1 &&
    fail "Uploaded non-existent file unexpectedly"

    log "Uploading to invalid Rogu URI"
    test_rogu upload $FILE1 https://foobar.org/rogu &&
    fail "Uploaded to invalid Rogu URI unexpectedly"

    log "Uploading file"
    test_rogu upload $FILE1 $URI1 || failed

    log "Uploading file again"
    test_rogu upload $FILE1 $URI1 || failed

    log "Upload a different file with the same name"
    test_rogu upload $FILE2 $URI1 &&
    fail "Uploaded a different file with the same name unexpectedly"
    success

    header "INSTALL FILE"

    log "Installing non-existent file"
    test_rogu install $RFILE1 $URI2 &&
    fail "Installed non-existent file unexpectedly"

    log "Installing from invalid Rogu URI"
    test_rogu install $RFILE1 https://foobar.org/rogu &&
    fail "Installed from invalid Rogu URI unexpectedly"

    log "Installing file"
    test_rogu install $FILE3 $URI1 || failed
    assert_equal $FILE1 $FILE3

    log "Installing file again"
    test_rogu install $FILE3 $URI1 || failed
    success

    header "SYNC FILE"

    log "Sync file from local to remote"
    test_rogu sync $FILE2 $URI2 || failed

    log "Sync file from local to remote again"
    test_rogu sync $FILE2 $URI2 || failed

    log "Sync file from remote to local"
    test_rogu sync $FILE4 $URI2 || failed
    assert_equal $FILE2 $FILE4
    success

    header "UPDATE FILE"

    log "Modify and update file 1"
    echo "Once upon a time, in a galaxy far far away..." > $FILE1
    test_rogu update || failed
    assert_equal $FILE1 $FILE3
    assert_equal $FILE2 $FILE4

    log "Modify and update file 2"
    echo "Lorem ipsum..." > $FILE2
    test_rogu update || failed
    test_rogu install -f $FILE4 $URI2 || failed
    assert_equal $FILE1 $FILE3
    assert_equal $FILE2 $FILE4
    success

    header "MOVE & DELETE FILE"

    local KEY1=$(run_rogu key $FILE1 $URI1)
    local KEY2=$(run_rogu key $FILE2 $URI2)
    local KEY3=$(run_rogu key $FILE3 $URI1)

    log "Try moving resource to an existing resource"
    test_rogu mv $KEY1 $FILE3 &&
    fail "Moved resource to an existing resource unexpectedly"

    log "Delete file 3"
    test_rogu rm --local $KEY3 || failed

    log "Move file 1 to file 3"
    test_rogu mv $KEY1 $FILE3 || failed

    log "Delete file 2"
    test_rogu rm --local --remote $KEY2 || failed

    log "Try updating"
    test_rogu update || failed
    success
}


# ------------------------------------------------------------------------------
# ARCHIVE

function archive {

    local URI1="ugor://archive/$RARCHIVE1"
    local URI2="ugor://archive/$RARCHIVE2?format=zip"

    header "UPLOAD ARCHIVE"

    log "Upload non-existent directory"
    test_rogu upload idont/exist $URI1 &&
    fail "Uploaded non-existent directory unexpectedly"

    log "Upload directory"
    test_rogu upload $DIR1 $URI1 || failed

    log "Upload directory again"
    test_rogu upload $DIR1 $URI1 || failed
    success

    header "INSTALL ARCHIVE"

    log "Install non-existent archive"
    test_rogu install $DIR3 ugor://archive/idont/exist &&
    fail "Installed non-existent archive unexpectedly"

    log "Install archive"
    test_rogu install $DIR3 $URI1 || failed
    assert_equal $DIR1 $DIR3

    log "Install archive again"
    test_rogu install $DIR3 $URI1 || failed
    success

    header "SYNC ARCHIVE"

    log "Sync archive from local to remote"
    test_rogu sync $DIR2 $URI2 || failed

    log "Sync archive from local to remote again"
    test_rogu sync $DIR2 $URI2 || failed

    log "Sync archive from remote to local"
    test_rogu sync $DIR4 $URI2 || failed
    assert_equal $DIR2 $DIR4
    success

    header "UPDATE ARCHIVE"

    log "Modifying and updating archive 1"
    echo "Once upon a time, in a galaxy far, far away..." > $DIR1/README.md
    test_rogu update || failed
    assert_equal $DIR1 $DIR3
    assert_equal $DIR2 $DIR4

    log "Modifying and updating archive 2"
    echo "Lorem ipsum dolor sit amet, consectetur adipiscing elit..." > $DIR2/README.md
    test_rogu update || failed
    test_rogu install -f $DIR4 $URI2 || failed
    assert_equal $DIR1 $DIR3
    assert_equal $DIR2 $DIR4
    success
}


# ------------------------------------------------------------------------------
# RUN TESTS

if ! [[ -d venv ]]; then
    err "No virtual environment found"
    exit 1
fi

ALL_COMMANDS=(ugor file archive)

# Determine which tests to run_rogu
if (( $# == 0 )); then
    COMMANDS=$(gum choose --no-limit \
        --header="Choose tests:" \
        --selected=$(echo $ALL_COMMANDS | tr ' ' ',') \
        ${=ALL_COMMANDS})
    [[ -n "$COMMANDS" ]] || exit 0
elif [[ $@ == "all" ]]; then
    COMMANDS=$ALL_COMMANDS
else
    COMMANDS=$@
fi

# Clean up after failed tests
[[ -d $DIR ]] && teardown

for CMD in ${=COMMANDS}; do
    # Some sanity checks
    if ! [[ $(test_rogu resources) =~ "No resources" ]]; then
        test_rogu resources
        fail "There shouldn't be any resources yet"
    fi
    FAIL_DUMP=1

    # Run the test
    setup
    $CMD
    teardown
done

echo "$fg_bold[green]\nAll tests passed! Have an awesome day! :)$reset_color"
